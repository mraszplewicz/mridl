grammar pl.mrasoft.mridl.Mridl with org.eclipse.xtext.common.Terminals

generate mridl "http://mrasoft.pl/mridl/Mridl"

Mridl:
	'namespace' nsUri=STRING
	(imports+=Import)*
	(operations+=Operation)*
	(types+=Type)*;

Import:
	"import" importURI=ImportMridlUri "as" nsPrefix=ID;

Type:
	ComplexType | SimpleType;

	//to jest do zmiany, ale jeszcze nie wiem jak
Operation:
	'operation' returnType=Element name=ID
	'(' (params+=Element
	(',' params+=Element)*)? ')';

Element:
	type=NamedTypeReference (multiplicity=Multiplicity)? name=ID;

Multiplicity:
	SpecifiedMultiplicity | UnspecifiedMultiplicity | Optional;

SpecifiedMultiplicity:
	{SpecifiedMultiplicity}
	"[" (lower=INT "..") (upper=INT | unbounded?="*") "]";

UnspecifiedMultiplicity:
	{UnspecifiedMultiplicity}
	"[]";

Optional:
	{Optional}
	"?";

NamedTypeReference:
	XsdBuiltinTypeReference | TypeReference;

TypeReference:
	DirectTypeReference | ImportedTypeReference;

DirectTypeReference:
	ref=[Type];

XsdBuiltinTypeReference:
	builtin=XsdBuiltinType;

ComplexType:
	"complex" name=ID "{" (elements+=Element)* "}";

SimpleType:
	"simple" name=ID;

ImportedTypeReference:
	^import=[Import|ID] ":" ref=[Type];

	//some types will not be fully supported
enum XsdBuiltinType:
	anyType
	| anySimpleType
	| anyAtomicType
	| anyURI
	| base64Binary
	| boolean
	| date
	| dateTime
	| dateTimeStamp
	| decimal
	| integer
	| long
	| int
	| short
	| byte
	| nonNegativeInteger
	| positiveInteger
	| unsignedLong
	| unsignedInt
	| unsignedShort
	| unsignedByte
	| nonPositiveInteger
	| negativeInteger
	| double
	| duration
	| dayTimeDuration
	| yearMonthDuration
	| float
	| gDay
	| gMonth
	| gMonthDay
	| gYear
	| gYearMonth
	| hexBinary
	| NOTATION
	| precisionDecimal
	| QName
	| string
	| normalizedString
	| token
	| language
	| Name
	| NCName
	| ENTITY
	| ID
	| IDREF
	| NMTOKEN
	| time
	| ENTITIES
	| IDREFS
	| NMTOKENS;

ImportMridlUri:
	STRING |
	(((ID | '..' | '.') '/')* ID '.mridl');
		