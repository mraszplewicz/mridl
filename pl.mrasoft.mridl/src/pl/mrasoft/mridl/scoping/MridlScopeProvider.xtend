/*
 * generated by Xtext
 */
package pl.mrasoft.mridl.scoping

import org.eclipse.xtext.scoping.Scopes
import pl.mrasoft.mridl.mridl.Mridl
import org.eclipse.emf.ecore.EReference
import org.eclipse.xtext.naming.QualifiedName
import org.eclipse.xtext.scoping.IScope
import pl.mrasoft.mridl.mridl.ImportedTypeReference
import pl.mrasoft.mridl.mridl.Type
import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.util.EcoreUtil
import pl.mrasoft.mridl.mridl.Import
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.EcoreUtil2

/**
 * This class contains custom scoping description.
 * 
 * see : http://www.eclipse.org/Xtext/documentation.html#scoping
 * on how and when to use it 
 *
 */
class MridlScopeProvider extends org.eclipse.xtext.scoping.impl.AbstractDeclarativeScopeProvider {

	def scope_Import(Mridl mridl, EReference eRef) {
		Scopes::scopeFor(mridl.imports, [QualifiedName::create(it.nsPrefix)], IScope::NULLSCOPE)
	}

	def scope_TypeReference_ref(ImportedTypeReference ref, EReference eRef) {
		createTopLevelTypeScope(ref, typeof(Type))
	}

	/**
	 * Returns a scope for a the {@code ref} cross-reference in a
	 * TopLevel<i>x</i>Reference object.
	 * 
	 * @param clazz
	 *            - the {@link Class} object corresponding to <i>x</i>; must
	 *            extend {@link TopLevelType}.
	 */
	def private createTopLevelTypeScope(ImportedTypeReference importRef, Class<? extends Type> clazz) {
		val schema = schema(importRef, importRef.^import.nsPrefix)
		Scopes::scopeFor(schema.types.filter(clazz))
	}

	def schema(EObject eObject, String prefix) {
		val thisMridl = EcoreUtil::getRootContainer(eObject) as Mridl
		for (Import importElt : thisMridl.getImports()) {
			if (importElt.nsPrefix == prefix) {
				return resolveImport(importElt)
			}
		}

		throw new IllegalArgumentException(
			"no import with prefix '" + prefix + "' in this schema"
		)
	}
	
	def resolveImport(Import importElt) {
		resolveImport(importElt.eResource, importElt.importURI)
	}

	def resolveImport(Resource resource, String uri) {
		val importResource = EcoreUtil2::getResource(resource, uri)
		importResource.contents.head as Mridl
	}

}
